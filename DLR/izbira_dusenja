using LinearAlgebra, Statistics, Random, Plots
using Measures  # <-- zaradi robov

# --- 1D Damped Rank Reduction (SSA-style) ------------------------------------

function hankel_matrix(d::Vector{T}) where T
    L = length(d)
    M = fld(L, 2) + 1
    N = L - M + 1
    H = Matrix{float(T)}(undef, M, N)
    @inbounds for i in 1:M, j in 1:N
        H[i, j] = d[i + j - 1]
    end
    return H
end

function inv_hankel(H::AbstractMatrix)
    M, N = size(H)
    L = M + N - 1
    T = float(eltype(H))
    d_est = zeros(T, L)
    counts = zeros(Int, L)
    @inbounds for i in 1:M, j in 1:N
        k = i + j - 1
        d_est[k] += H[i, j]
        counts[k] += 1
    end
    @inbounds for k in 1:L
        d_est[k] /= counts[k]
    end
    return d_est
end

function damped_tsvd(M::AbstractMatrix, K::Int, damp::Real)
    F = svd(M; full=false)
    r = min(K, length(F.S))
    S1 = F.S[1:r]
    S2 = F.S[r+1:end]
    δ̂ = isempty(S2) ? zero(eltype(S1)) : maximum(S2)

    S1safe = max.(S1, eps(eltype(S1)))
    t = 1 .- (δ̂^damp) .* (S1safe .^ (-damp))
    t = clamp.(t, 0, 1)
    Td = Diagonal(t)

    U1  = F.U[:, 1:r]
    Σ1  = Diagonal(S1)
    V1t = F.Vt[1:r, :]
    return U1 * (Σ1 * Td) * V1t
end

function damped_rank_reduction_1d(d::Vector, K::Int, damp::Real)
    D = hankel_matrix(d)
    M_damped = damped_tsvd(D, K, damp)
    inv_hankel(M_damped)
end

compute_snr(clean::Vector, estimated::Vector) =
    10 * log10(sum(abs2, clean) / sum(abs2, clean .- estimated))

# --- testni signal ---
t = 1:50
clean_signal = [cos(0.2 * i) + 6 * (sin(0.4 *i)) + 2 * sin(0.6 *i) for i in t]

rng = MersenneTwister(1234)
noise = 0.4 .* randn(rng, length(clean_signal))
noisy_signal = clean_signal .+ noise

K = 5
damp_values = 1:100
snr_single_damp = zeros(Float64, length(damp_values))

for (idx, Nexp) in enumerate(damp_values)
    d1 = damped_rank_reduction_1d(noisy_signal, K, Nexp)
    snr_single_damp[idx] = compute_snr(clean_signal, d1)
end

i_best = argmax(snr_single_damp)
N_best = damp_values[i_best]
SNR_best = snr_single_damp[i_best]
println("Najboljši faktor dušenja: N* = $N_best, SNR = $(round(SNR_best, digits=3)) dB")

den_best = damped_rank_reduction_1d(noisy_signal, K, N_best)

p_sig = plot(t, clean_signal;
    label="Čisti", lw=2,
    left_margin=10mm, bottom_margin=6mm)
plot!(p_sig, t, noisy_signal; label="Šumni", lw=2, ls=:dash)
plot!(p_sig, t, den_best; label="dMSSA (K=2, N=$N_best)", lw=2, ls=:dot)
xlabel!(p_sig, "Čas"); ylabel!(p_sig, "Amplituda")
title!(p_sig, "dMSSA razšumljanje – najboljši faktor dušenja (en prehod)")

pN = plot(damp_values, snr_single_damp;
    label="En prehod (K=2)", lw=2, marker=:circle,
    left_margin=10mm, bottom_margin=6mm)
vline!(pN, [N_best]; ls=:dash, label="N*")
xlabel!(pN, "Faktor dušenja N"); ylabel!(pN, "SNR (dB)")
title!(pN, "Vpliv N na SNR (en prehod, K=2)")

plt = plot(pN, p_sig;
    layout=(2,1),
    size=(900,700),
    outer_margin=6mm,
    left_margin=10mm)

display(plt)
savefig(plt, "dlr_damping_sweep.pdf")
